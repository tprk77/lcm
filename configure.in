AC_PREREQ(2.50)
AC_INIT(lcm/lcm.h)
AM_CONFIG_HEADER(config.h)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(lcm, 0.0.1)

AC_PROG_CC
AM_PATH_GLIB_2_0(,,,gthread)
AC_PATH_PROGS(JAR, [jar])
AC_PATH_PROGS(JAVAC, [javac])

if test "x$JAR" = x -o "x$JAVAC" = x; then
    AC_MSG_WARN([Java support disabled])
else
    JAVA_FOUND="yes"
fi

AM_CONDITIONAL(HAVE_JAVA, test "x$JAVA_FOUND" = "xyes")

dnl ---------------------------------------------------------------------------
dnl When making a release:
dnl  1. If the library source code has changed at all since the last release,
dnl     then increment REVISION.
dnl  2. If the interface has been changed since the last release, increment
dnl     CURRENT, and set REVISION to 0.
dnl  3. If the interface changes consist solely of additions, increment AGE.
dnl  4. If the interface has removed or changed elements, set AGE to 0.
dnl ---------------------------------------------------------------------------
LIBLCM_AGE=0
LIBLCM_REVISION=0
LIBLCM_CURRENT=0
AC_SUBST(LIBLCM_AGE)
AC_SUBST(LIBLCM_REVISION)
AC_SUBST(LIBLCM_CURRENT)
LIBLCM_VERSION_INFO=$LIBLCM_CURRENT:$LIBLCM_REVISION:$LIBLCM_AGE
AC_SUBST(LIBLCM_VERSION_INFO)

AM_DISABLE_STATIC
AM_PROG_LIBTOOL

dnl ------------------
dnl Python support
dnl ------------------
AC_PYTHON_DEVEL
AM_PATH_PYTHON([2.4],, [:])
AM_CONDITIONAL([HAVE_PYTHON], [test "$pythonexists" = "yes"])

pylcmexecdir="$pyexecdir/$PACKAGE"
AC_SUBST(pylcmexecdir)

dnl ---------------------------------------------------------------------------
dnl i18n support
dnl ---------------------------------------------------------------------------
ALL_LINGUAS=""

dnl The gettext domain of the library
GETTEXT_PACKAGE=${PACKAGE}
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[The gettext domain for the library])
AC_SUBST(GETTEXT_PACKAGE)
AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION(0.14.3)

AM_ICONV()

dnl ---------------------------------------------------------------------------
dnl Warnings
dnl ---------------------------------------------------------------------------
if test "x$GCC" = "xyes"; then
    CFLAGS="$CFLAGS -g -Wall -std=gnu99 -D_FILE_OFFSET_BITS=64 -D_LARGEFILE_SOURCE -D_REENTRANT -Wno-unused-parameter -Wno-format-zero-length -pthread -Wshadow"
    LDFLAGS="$LDFLAGS -lm"
    AC_SUBST(CFLAGS)
    AC_SUBST(LDFLAGS)
fi
LIBTOOL="$LIBTOOL --silent"
AC_SUBST(LIBTOOL)

AC_OUTPUT([
  Makefile
  lcm/Makefile
  lcm/lcm.pc
  liblcm-test/Makefile
  lcm-logger/Makefile
  lcmgen/Makefile
  lcm-java/Makefile
  lcm-python/Makefile
  m4/Makefile
  po/Makefile.in
])

if test "x$JAVA_FOUND" = "xyes"; then
    JAVA_STR="Enabled"
else
    JAVA_STR="Disabled"
fi

if test "$pythonexists" = "yes"; then
    PYTHON_STR="Enabled"
else
    PYTHON_STR="Disabled"
fi

echo -n "

Configuration (LCM):

	Source code location:  ${srcdir}
	Compiler:              ${CC}
	Java Support:          ${JAVA_STR}
	Python Support:        ${PYTHON_STR}
"
